plugins {
    id "com.github.johnrengelman.shadow"
}

apply plugin: 'maven-publish'

version = '1.4.7'
archivesBaseName = 'Hyggdrasil-API'

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = 'hyggdrasil-api'
                version = project.version
                name = 'HyggdrasilAPI'
                description = 'Official API of Hyggdrasil'
                url = 'https://github.com/Hyriode/Hyggdrasil'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GITHUB_USERNAME')
                password System.getenv('GITHUB_TOKEN')
            }
            name = "Hyggdrasil_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hyggdrasil"
        }
    }
}

dependencies {
    api 'org.jetbrains:annotations:24.0.0'
    api 'redis.clients:jedis:4.3.1'
    api 'com.google.code.gson:gson:2.10.1'
}